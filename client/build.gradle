buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'com.github.node-gradle:gradle-node-plugin:2.2.3'
  }
}

apply plugin: 'base'
apply plugin: 'com.github.node-gradle.node'

node {
  /* gradle-node-plugin configuration
   * https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md !!No longer maintained,
   * forked to below repo.
   * https://github.com/node-gradle/gradle-node-plugin/blob/master/docs/node.md
   * Task name pattern:
   * ./gradlew npm_<command> Executes an NPM command.
   */
  version = '12.16.1'
  npmVersion = '6.13.4'
  download = false
}

task packageNpmApp (type: Zip) {
  dependsOn npm_run_build
  archiveBaseName = "${rootProject.ngArtifact}"
  extension 'jar'
  destinationDirectory = file("${projectDir}/dist")
  from('build') {
    // optional path under which output will be visible in Java Classpath, e.g. static resources path
    into 'static'
  }
}

configurations {
  npmResources
}

configurations.default.extendsFrom(configurations.npmResources)
assemble.dependsOn packageNpmApp

artifacts {
  npmResources(packageNpmApp.archiveFile) {
    builtBy packageNpmApp
    type "jar"
  }
}

clean {
  delete packageNpmApp.archiveFile
  delete file("${projectDir}/dist")
}
